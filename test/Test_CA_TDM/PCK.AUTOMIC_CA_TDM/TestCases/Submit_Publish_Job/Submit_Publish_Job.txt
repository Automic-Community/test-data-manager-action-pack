*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        submit_publish_job
Test Template     Submit Publish Job Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    JSONContent
Submit Publish Job with valid JSON Content
                      [Tags]    main_scenarios
                      OK        ${JSON_Content}

Submit Publish Job with invalid JSON Content
                      NOK       ${Invalid_JSON_Content}

Submit Publish Job with invalid format for JSON Content
                      NOK       Invalid

Assert JSON Content as Mandatory Field
                      NOK       ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "SUBMIT PUBLISH JOB"*******
    Log    *******Connect to AE*******
    Connect AE

Submit Publish Job Template
    [Arguments]    ${OK_NOK}    ${JSONContent}
    [Documentation]    This test is used for generation of test data on TDM
    ...    -Precondition: To execute valid test, Valid JSON Content should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${JSONContent}: This field specifies the JSON Content used for generating test data.
    Generate String
    Init Action    ${_SUBMIT_PUBLISH_JOB_ACTION}
    Connect To TDM
    Run Keyword If    '${JSONContent}' == 'Invalid'    Action Set    &UC4RB_TDM_JSON_CONTENT#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TDM_JSON_CONTENT#    ${JSONContent}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_TDM_JOBID#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_TDM_JOBID#

Teardown
    Log    *******End Test "SUBMIT PUBLISH JOB"*******
